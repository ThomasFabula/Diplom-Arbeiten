/COM  Datei: BALK2.F18

/com  Silizium-Einfachbalken
/com  Daten fuer den GMS-Kraftsensor
/com  ********************************************
/prep7
/title, Silizium-Balkenresonator

/COM  Materialwerte Si<110>
SIEM=1.689e11      ! mittlerer E-Modul    [N/m2]
SIPO=0.0625        ! mittlere Poissonzahl
SIRO=2329          ! Materialdichte       [kg/m3]
/COM    Materialeigenschaften Silizium
MP,EX,1,SIEM
MP,NUXY,1,SIPO
MP,DENS,1,SIRO

/com  ********************************************
/com  Parameter (alle Masse in m)
LANG=10e-3         ! Balkenlaenge [m]
DISI=50e-6         ! Balkendicke [m]
BREI=1e-3          ! Balkenbreite [m]
DWAF=380e-6        ! Waferdicke  (3 Zoll)
AUFH=2.035e-3      ! Balkenaufhaengung unten

/com  Hilfs-Parameter
ausl=-0.1e-6       ! dyn. Balkenauslenkung (therm.Anreg.)
elgr=200e-6        ! Elementgroesse
z1=dwaf-disi       ! Hoehe der Aufhaengung
x1=(dwaf-disi)/1.41 ! Schraege der Aufh.
len=lang/2
xges=aufh+x1+len
xg=2*xges

/com  ********************************************
/COM  Geometrie erstellen
k,1
k,2,aufh
k,3,aufh+x1,,z1
k,4,aufh+x1+len,,z1
k,5,aufh+x1+len,,dwaf
k,6,aufh+x1,,dwaf
k,7,0,0,dwaf
k,8,0,0,z1
kgen,2,all,,,,BREI

/COM  Volumen generieren
v,1,2,3,8,9,10,11,16
v,8,3,6,7,16,11,14,15
v,3,4,5,6,11,12,13,14
local,11,,xges
vsym,,all
csys

/com  Vernetzen
et,1,45
elsi,elgr
vmesh,all

/com  Knoten mergen
nsel,x,xges
nrsel,z,dwaf
nummrg,node,20e-6
nsel,x,xges
nrsel,z,z1
nummrg,node,20e-6
nall

/com  Aufhaengung einspannen
nsel,z
d,all,all
/com  Krafteinleitung ermoeglichen
nsel,x,xges,xg
ddel,all
nrsel,z
d,all,uz
d,all,uy
nall

/com  statische Auslenkung vorgeben
/com nsel,x,xges
/com nrsel,z,dwaf
/com d,all,uz,ausl
/COM nall

/COM  ANSYS-Analyseoptionen
! kan,2
! kay,2,5
! kay,3,-10
total,100
wsort

/type,1,3
/pnum,kpoi,1
/pbc,all,2         ! alle Randbedingungen anzeigen
! /view,1,,-.5,.2    ! schraege Ansicht
/view,1,1,1,1
! /vup,,z
/com  ********************************************
